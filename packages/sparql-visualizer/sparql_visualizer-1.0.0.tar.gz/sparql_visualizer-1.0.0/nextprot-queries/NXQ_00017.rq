#id:NXQ_00017
#title:Proteins larger than 1000 amino acids that are located in the nucleus and expressed in the nervous system
#tags:expression,sequence,subcellular location,tutorial
#acs:A3KN83,A4D0V7,A5PL33,A6NHR9,A8MQ14,B1AJZ9,O00159,O00203,O00267,O00268
#count:1264
#time:1.1

select distinct ?entry where {
  values ?nucloc {cv:GO_0005634 cv:SL-0191} # GO and SL values for nucleus
  
  ?entry :isoform ?iso.
  ?iso :cellularComponent ?loc .
  ?loc :term/:childOf ?nucloc.
  filter not exists {?loc :negativeEvidence ?negev} # No negative localization evidence
  ?iso :detectedExpression/:term/:childOf cv:TS-1313.
  ?iso :sequence/:length ?len.
  filter (?len>1000)
}
