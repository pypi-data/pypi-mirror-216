#id:NXQ_00091
#title:Proteins which are targets of drugs for cardiac therapy
#comment:Federated query with DrugBank
#comment:Query based on the drug classification ATC Codes implemented in DrugBank
#comment:This is the same federated query with DrugBank as NXQ_00096 but with a more up-to-date endpoint
#tags:drug,federated query
#acs:P09172,Q07343,P00439, ...
#count:92
#time:7

select distinct ?entry where {
  service <http://drugbank.bio2rdf.org/sparql> {
    select distinct ?uniprot WHERE {
	?drug <http://bio2rdf.org/drugbank_vocabulary:target> ?drugTarget .  
	?drug <http://bio2rdf.org/drugbank_vocabulary:x-atc> ?atcCode.
    ?drugTarget <http://bio2rdf.org/drugbank_vocabulary:x-uniprot> ?uniprot .
	filter(!contains(str(?uniprot),"_"))
	filter(contains(str(?atcCode), "atc:C01" )) # ATC starting with C01 means therapeutic subgroup for cardiac therapy
    }
  }
  BIND (IRI(CONCAT("http://nextprot.org/rdf/entry/NX_",substr(str(?uniprot),28,6))) as ?entry) # cast drugbank id to neXtprot entry
}