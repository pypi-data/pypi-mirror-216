#id:NXQ_00095
#title:Proteins which are targets of antipsychotic drugs and highly expressed in brain
#comment:This is the same federated query with DrugBank as NXQ_00094 but with a more up-to-date endpoint
#tags:drug,expression,federated query
#acs:P08172,P08908, P14867, ...
#count: 12
#time: 1.3

select distinct ?entry where {
  service <http://drugbank.bio2rdf.org/sparql> {
    select distinct ?uniprot WHERE {
    ?drug <http://bio2rdf.org/drugbank_vocabulary:category> ?drugCat .
	?drug <http://bio2rdf.org/drugbank_vocabulary:target> ?drugTarget .  
    ?drugTarget <http://bio2rdf.org/drugbank_vocabulary:x-uniprot> ?uniprot .
	filter(!contains(str(?uniprot),"_"))
	filter(contains(str(?drugCat),"Antipsychotic"))
    }
  }
  BIND (IRI(CONCAT("http://nextprot.org/rdf/entry/NX_",substr(str(?uniprot),28,6))) as ?entry) # cast drugbank id to neXtprot entry
  ?entry :isoform/:expressionProfile ?expr.
  ?expr :term/:childOf cv:TS-0095.
  ?expr :evidence / :expressionLevel :High.
}
