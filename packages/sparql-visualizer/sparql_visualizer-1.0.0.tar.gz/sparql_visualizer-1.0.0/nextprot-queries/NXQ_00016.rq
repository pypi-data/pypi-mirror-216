#id:NXQ_00016
#title:Proteins with a mature chain of less than 1000 amino acids which are secreted and do not contain cysteines in the mature chain
#comment:The search takes into account all isoforms so sometimes you may see cysteines in a displayed isoform but at least one of the isoform does not contain any cysteine.
#tags:processing,sequence,subcellular location,tutorial
#acs:P10163,P01258,P01270
#count:371/220
#time:1.9

select distinct ?entry where {
  values ?sloc {cv:GO_0005576 cv:SL-0243} # GO and SL values for secreted
  ?entry :isoform ?iso.
  ?iso :cellularComponent ?anno .
  ?anno :term /:childOf ?sloc .
  filter not exists { ?anno :negativeEvidence ?_ }
  #
  # filter not exists {?iso :cellularComponent /:term cv:GO_0070062 .
  #   filter not exists {?iso :cellularComponent /:term /:childOf  cv:SL-0243 .}
  # } 
  # you can uncomment this to filters out extracellular exosome only location (2240 entries), 
  # most of them evidenced only by large-scale proteomic analysis  
  #
  ?iso :sequence / :chain ?seq. 
  ?iso :matureProtein [ :start ?mstart ; :end ?mend]
  filter ((?mend-?mstart > 0) && (?mend-?mstart < 1000))	 
  bind (?mend - ?mstart + 1 as ?mlen)	 
  bind (substr(?seq, ?mstart, ?mlen) as ?mseq)	 
  filter (!regex (?mseq,'C'))
}
