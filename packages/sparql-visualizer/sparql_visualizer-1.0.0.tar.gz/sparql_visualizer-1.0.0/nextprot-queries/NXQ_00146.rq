#id:NXQ_00146
#title:Proteins having at least 2 proteotypic peptides of 7 or 8aa but no proteotypic peptide >= 9 aa
#tags:peptide,proteomics,tutorial
#acs:P13164,Q16777,Q14430
#count:5
#time: 16.1

select distinct ?entry where {
?entry :isoform / :peptideMapping ?pm.
?pm :proteotypic true .
?pm :peptideName ?pepid .
#?pm :start ?pos1 ; :end ?pos2 .
#filter((?pos2 - ?pos1 + 1) >= 7)  # This is not absolutely required for current query since 7 is the minimum length for accepted proteotypic peptides
filter not exists {
 ?entry :isoform / :peptideMapping ?pm2.
 ?pm2 :start ?p1 ; :end ?p2 .
 ?pm2 :proteotypic true .
 filter((?p2 - ?p1 + 1) >= 9)
 }
}
group by ?entry having(count (distinct ?pepid) >= 2)
