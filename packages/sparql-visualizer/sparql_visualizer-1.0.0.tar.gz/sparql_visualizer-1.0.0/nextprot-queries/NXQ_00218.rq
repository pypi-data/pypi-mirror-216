#id:NXQ_00218
#title:Proteins for which different splice isoforms have a different subcellular location or function
#tags:function,isoforms,subcellular location,tutorial
#A0A1B0GVQ0,A0AVT1 ,A0PJK1...
#count:891
#time:7.7

select distinct ?entry where {
 ?entry :isoform ?iso1, ?iso2.
 ?iso1 :swissprotDisplayed true .
 ?iso2 :swissprotDisplayed false .
 {
 ?iso2 :function / :term ?functerm .
 filter (?functerm != cv:GO_0005515) #protein-binding
 filter not exists {?iso1 :function / :term ?functerm .}
 }
 UNION
 {
 ?iso2 :functionInfo / rdfs:comment ?functext .
 filter not exists {?iso1 :functionInfo / rdfs:comment ?functext .}
 }
 UNION
 {
 ?iso2 :cellularComponent / :term ?locterm .
 filter not exists {?iso1 :cellularComponent / :term ?locterm}
 }
}
