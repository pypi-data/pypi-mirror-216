#id:NXQ_09624
#title:Check there are no entries which are PE2-PE4 with a ptm information annotation with positive GOLD experimental evidence from neXtProt and "phosphorylated" in the text
#tags:QC,protein existence,PE
#acs:
#count:0
#time:0.924

select distinct ?entry where {
  ?entry a :Entry.
  ?entry :existence ?pe.
  filter ( ?pe not in (:Evidence_at_protein_level,:Uncertain ))
  ?entry :isoform ?iso.
  ?iso :ptmInfo ?ptm.
  ?ptm rdfs:comment ?txt.
  filter (regex (?txt,'phosphorylated')) 
  ?ptm :evidence ?ev.
  ?ev :assignedBy source:NextProt.
  ?ev :quality :GOLD.
  filter not exists {?ev :negative 1.}
  ?ev :evidenceCode cv:ECO_0000006. # experimental evidence
}
