#id:NXQ_00084
#title:Proteins whose genes are on chromosome 18 and that are experimentally (cv:ECO_0000269) known to be glycosylated or phosphorylated or acetylated
#tags:method, chromosomal location,glycosylation,phosphorylation,PTM,tutorial
#acs:O95427,P49257,P25391
#count:102
#time:0.12

select distinct ?entry {
  ?entry :gene / :chromosome "18"^^xsd:string.
  ?entry :isoform ?iso.
  {
    ?iso :glycosylationSite ?ptm1.
    ?ptm1 :evidence /:evidenceCode / :childOf cv:ECO_0000269
  } 
  union {
    ?iso :modifiedResidue ?ptm2.
    ?ptm2 rdfs:comment ?com.
    ?ptm2 :evidence / :evidenceCode / :childOf cv:ECO_0000269
    filter (regex(?com, '^phospho|acetyl','i'))
  }
}
