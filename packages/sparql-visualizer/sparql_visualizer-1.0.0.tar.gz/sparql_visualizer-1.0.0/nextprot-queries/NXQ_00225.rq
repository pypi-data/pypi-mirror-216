#id:NXQ_00225
#title:Proteins with high proline content
#comment:By setting the AAcnt filter to 0 you will eventually get the longest sequences without a given AA, order with desc(?seqlen).
#tags:sequence,snorql-only,tutorial
#acs:Q9BYE4,P02814,C9JFL3
#count:10
#time:80

select distinct ?entry ?AAcnt ?seqlen ?AAdensity where {
 ?entry :isoform ?iso.
 ?iso :swissprotDisplayed true .
 ?iso :sequence / :chain ?seq .
 ?iso :sequence / :length ?seqlen .
 bind( strlen( replace( str(?seq), "[^P]", "")) as ?AAcnt) # replace all non-Proline by empty string
 bind(xsd:float(?AAcnt)/xsd:float(?seqlen) as ?AAdensity)
 filter(?AAcnt > 10)
}
order by desc(?AAdensity)
limit 100 # will bring-up the 100 most relevant cases
