#id:NXQ_09458
#title:Check there are no entries with a ptm info annotation referring to ADP-ribosylation that do not have the keyword ADP-ribosylation (KW-0013)
#tags:QC,keyword
#acs:P13639,Q9UGN5
#count:2
#time:0.958

select distinct ?entry where {  # Include ?text if result is not 0 to identify problem site
  ?entry :isoform ?iso.
  ?iso :ptmInfo /rdfs:comment ?text.
  filter(contains(?text,"ADP-ribosylat")). # Finds ADP-ribosylated and ADP-ribosylation
  filter not exists {?iso :ptmInfo /:negativeEvidence ?negev} # No negative annotations from neXtProt
  filter not exists {?entry :isoform /:keyword /:term cv:KW-0013.}  
}
